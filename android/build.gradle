group = "com.example.airwallex_payment_flutter"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "1.8.22"
    ext.airwallex_version = "6.0.4"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.1.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    if (project.android.hasProperty("namespace")) {
        namespace = "com.example.airwallex_payment_flutter"
    }

    compileSdk = 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21

        def environment = project.hasProperty("env") ? project.env : ""
        buildConfigField "String", "ENVIRONMENT", "\"${environment}\""
        resValue "string", "environment", "${environment}"
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }
    buildFeatures {
        buildConfig true
    }
}

if (file('local.dependencies.gradle').exists()) {
    apply from: 'local.dependencies.gradle'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.activity:activity-ktx:1.9.2'

    implementation "io.github.airwallex:payment:$airwallex_version"
    implementation "io.github.airwallex:payment-card:$airwallex_version"
    implementation "io.github.airwallex:payment-redirect:$airwallex_version"
    implementation "io.github.airwallex:payment-wechat:$airwallex_version"
    implementation "io.github.airwallex:payment-googlepay:$airwallex_version"
}
